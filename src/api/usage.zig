const std = @import("std");
const auth = @import("../auth/auth.zig");
const chatgpt_http = @import("http.zig");
const registry = @import("../registry/root.zig");
const app_runtime = @import("../core/runtime.zig");
const session = @import("../session.zig");

pub const default_usage_endpoint = "https://chatgpt.com/backend-api/wham/usage";
pub const reset_credits_endpoint = "https://chatgpt.com/backend-api/wham/rate-limit-reset-credits";

pub const UsageFetchResult = struct {
    snapshot: ?registry.RateLimitSnapshot,
    status_code: ?u16,
    error_code: ?ResponseErrorCode = null,
    missing_auth: bool = false,
};

pub const max_response_error_code_bytes: usize = 64;

pub const ResponseErrorCode = struct {
    bytes: [max_response_error_code_bytes]u8 = undefined,
    len: usize = 0,

    pub fn text(self: *const @This()) []const u8 {
        return self.bytes[0..self.len];
    }
};

pub const BatchUsageFetchResult = struct {
    snapshot: ?registry.RateLimitSnapshot = null,
    status_code: ?u16 = null,
    error_code: ?ResponseErrorCode = null,
    missing_auth: bool = false,
    error_name: ?[]const u8 = null,

    pub fn deinit(self: *@This(), allocator: std.mem.Allocator) void {
        if (self.snapshot) |*snapshot| {
            registry.freeRateLimitSnapshot(allocator, snapshot);
            self.snapshot = null;
        }
    }
};

const UsageHttpResult = struct {
    body: []u8,
    status_code: ?u16,
};

const ParsedCurlHttpOutput = struct {
    body: []const u8,
    status_code: ?u16,
};

pub fn fetchActiveUsage(allocator: std.mem.Allocator, codex_home: []const u8) !?registry.RateLimitSnapshot {
    const result = try fetchActiveUsageDetailed(allocator, codex_home);
    return result.snapshot;
}

pub fn fetchActiveUsageDetailed(allocator: std.mem.Allocator, codex_home: []const u8) !UsageFetchResult {
    const auth_path = try registry.activeAuthPath(allocator, codex_home);
    defer allocator.free(auth_path);

    return try fetchUsageForAuthPathDetailed(allocator, auth_path);
}

pub fn fetchUsageForAuthPath(allocator: std.mem.Allocator, auth_path: []const u8) !?registry.RateLimitSnapshot {
    const result = try fetchUsageForAuthPathDetailed(allocator, auth_path);
    return result.snapshot;
}

pub fn fetchUsageForAuthPathDetailed(allocator: std.mem.Allocator, auth_path: []const u8) !UsageFetchResult {
    const info = try auth.parseAuthInfo(allocator, auth_path);
    defer info.deinit(allocator);

    if (info.auth_mode == .apikey) return .{ .snapshot = null, .status_code = null };
    if (info.auth_mode != .chatgpt) return .{ .snapshot = null, .status_code = null, .missing_auth = true };
    const access_token = info.access_token orelse return .{ .snapshot = null, .status_code = null, .missing_auth = true };
    const chatgpt_account_id = info.chatgpt_account_id orelse return .{ .snapshot = null, .status_code = null, .missing_auth = true };

    return try fetchUsageForTokenDetailed(allocator, default_usage_endpoint, access_token, chatgpt_account_id);
}

pub fn fetchUsageForAuthPathsDetailedBatch(
    allocator: std.mem.Allocator,
    auth_paths: []const []const u8,
    max_concurrency: usize,
) ![]BatchUsageFetchResult {
    const results = try allocator.alloc(BatchUsageFetchResult, auth_paths.len);
    errdefer allocator.free(results);
    for (results) |*result| result.* = .{};

    if (auth_paths.len == 0) return results;

    var arena_state = std.heap.ArenaAllocator.init(std.heap.smp_allocator);
    defer arena_state.deinit();
    const arena = arena_state.allocator();

    var requests = std.ArrayList(chatgpt_http.BatchRequest).empty;
    defer requests.deinit(arena);

    const request_indexes = try arena.alloc(?usize, auth_paths.len);
    for (request_indexes) |*slot| slot.* = null;

    for (auth_paths, 0..) |auth_path, idx| {
        var info = auth.parseAuthInfo(arena, auth_path) catch |err| {
            results[idx].error_name = @errorName(err);
            continue;
        };
        defer info.deinit(arena);

        if (info.auth_mode == .apikey) {
            continue;
        }
        if (info.auth_mode != .chatgpt) {
            results[idx].missing_auth = true;
            continue;
        }
        const access_token = info.access_token orelse {
            results[idx].missing_auth = true;
            continue;
        };
        const chatgpt_account_id = info.chatgpt_account_id orelse {
            results[idx].missing_auth = true;
            continue;
        };

        var existing_request_index: ?usize = null;
        for (requests.items, 0..) |request, request_idx| {
            if (std.mem.eql(u8, request.access_token, access_token) and
                std.mem.eql(u8, request.account_id, chatgpt_account_id))
            {
                existing_request_index = request_idx;
                break;
            }
        }

        if (existing_request_index) |request_idx| {
            request_indexes[idx] = request_idx;
            continue;
        }

        try requests.append(arena, .{
            .access_token = try arena.dupe(u8, access_token),
            .account_id = try arena.dupe(u8, chatgpt_account_id),
        });
        request_indexes[idx] = requests.items.len - 1;
    }

    if (requests.items.len == 0) return results;

    var http_results = try chatgpt_http.runGetJsonBatchCommand(
        allocator,
        default_usage_endpoint,
        requests.items,
        max_concurrency,
    );
    defer http_results.deinit(allocator);

    for (request_indexes, 0..) |request_idx, result_idx| {
        const unique_idx = request_idx orelse continue;
        const http_result = http_results.items[unique_idx];
        results[result_idx].status_code = http_result.status_code;
        results[result_idx].error_code = parseNonSuccessErrorCode(allocator, http_result.status_code, http_result.body);
        switch (http_result.outcome) {
            .ok => {
                if (http_result.body.len == 0 or isNonSuccessStatus(http_result.status_code)) continue;
                results[result_idx].snapshot = parseUsageResponse(allocator, http_result.body) catch |err| {
                    results[result_idx].error_name = @errorName(err);
                    continue;
                };
            },
            .timeout => results[result_idx].error_name = @errorName(error.TimedOut),
            .failed => results[result_idx].error_name = @errorName(error.RequestFailed),
        }
    }

    // Optional enrichment: failure must not discard the main usage response.
    var has_reset_credits = false;
    for (results) |result| {
        if (result.snapshot) |snapshot| {
            if ((snapshot.reset_credits orelse 0) > 0) has_reset_credits = true;
        }
    }
    if (has_reset_credits) {
        if (chatgpt_http.runGetJsonBatchCommand(allocator, reset_credits_endpoint, requests.items, max_concurrency)) |reset_results_value| {
            var reset_results = reset_results_value;
            defer reset_results.deinit(allocator);
            const now = std.Io.Timestamp.now(app_runtime.io(), .real).toSeconds();
            for (request_indexes, 0..) |request_idx, result_idx| {
                const unique_idx = request_idx orelse continue;
                if (results[result_idx].snapshot) |*snapshot| {
                    if ((snapshot.reset_credits orelse 0) <= 0) continue;
                    const response = reset_results.items[unique_idx];
                    if (response.outcome != .ok or isNonSuccessStatus(response.status_code)) continue;
                    snapshot.reset_credits_expires_at = parseResetCreditExpiry(allocator, response.body, now) catch null;
                }
            }
        } else |_| {}
    }

    return results;
}

pub fn fetchUsageForToken(
    allocator: std.mem.Allocator,
    endpoint: []const u8,
    access_token: []const u8,
    account_id: []const u8,
) !?registry.RateLimitSnapshot {
    const result = try fetchUsageForTokenDetailed(allocator, endpoint, access_token, account_id);
    return result.snapshot;
}

pub fn fetchUsageForTokenDetailed(
    allocator: std.mem.Allocator,
    endpoint: []const u8,
    access_token: []const u8,
    account_id: []const u8,
) !UsageFetchResult {
    const http_result = try runUsageCommand(allocator, endpoint, access_token, account_id);
    defer allocator.free(http_result.body);
    const error_code = parseNonSuccessErrorCode(allocator, http_result.status_code, http_result.body);
    if (http_result.body.len == 0) {
        return .{ .snapshot = null, .status_code = http_result.status_code, .error_code = error_code };
    }
    if (isNonSuccessStatus(http_result.status_code)) {
        return .{ .snapshot = null, .status_code = http_result.status_code, .error_code = error_code };
    }

    var snapshot = try parseUsageResponse(allocator, http_result.body);
    if (std.mem.eql(u8, endpoint, default_usage_endpoint)) {
        if (snapshot) |*value| {
            if ((value.reset_credits orelse 0) > 0) {
                if (runUsageCommand(allocator, reset_credits_endpoint, access_token, account_id)) |response| {
                    defer allocator.free(response.body);
                    if (!isNonSuccessStatus(response.status_code)) {
                        value.reset_credits_expires_at = parseResetCreditExpiry(allocator, response.body, std.Io.Timestamp.now(app_runtime.io(), .real).toSeconds()) catch null;
                    }
                } else |_| {}
            }
        }
    }
    return .{
        .snapshot = snapshot,
        .status_code = http_result.status_code,
        .error_code = error_code,
    };
}

fn isNonSuccessStatus(status_code: ?u16) bool {
    const status = status_code orelse return false;
    return status < 200 or status > 299;
}

pub fn parseNonSuccessErrorCode(
    allocator: std.mem.Allocator,
    status_code: ?u16,
    body: []const u8,
) ?ResponseErrorCode {
    if (!isNonSuccessStatus(status_code) or body.len == 0) return null;

    var parsed = std.json.parseFromSlice(std.json.Value, allocator, body, .{}) catch return null;
    defer parsed.deinit();

    const root_obj = switch (parsed.value) {
        .object => |obj| obj,
        else => return null,
    };
    const code = codeFromNestedObject(root_obj, "error") orelse
        codeFromNestedObject(root_obj, "detail") orelse
        return null;
    if (code.len == 0) return null;

    var out: ResponseErrorCode = .{};
    out.len = @min(code.len, out.bytes.len);
    @memcpy(out.bytes[0..out.len], code[0..out.len]);
    return out;
}

fn codeFromNestedObject(root_obj: std.json.ObjectMap, key: []const u8) ?[]const u8 {
    const nested_obj = switch (root_obj.get(key) orelse return null) {
        .object => |obj| obj,
        else => return null,
    };
    return switch (nested_obj.get("code") orelse return null) {
        .string => |value| value,
        else => null,
    };
}

pub fn parseUsageResponse(allocator: std.mem.Allocator, body: []const u8) !?registry.RateLimitSnapshot {
    var parsed = try std.json.parseFromSlice(std.json.Value, allocator, body, .{ .parse_numbers = false });
    defer parsed.deinit();

    const root_obj = switch (parsed.value) {
        .object => |obj| obj,
        else => return null,
    };

    var snapshot = registry.RateLimitSnapshot{
        .primary = null,
        .secondary = null,
        .credits = null,
        .reset_credits = null,
        .plan_type = null,
    };

    if (root_obj.get("plan_type")) |plan_type| {
        snapshot.plan_type = parsePlanType(plan_type);
    }
    if (root_obj.get("credits")) |credits| {
        snapshot.credits = try parseCredits(allocator, credits);
    }
    if (root_obj.get("rate_limit_reset_credits")) |reset_credits| {
        snapshot.reset_credits = parseResetCredits(reset_credits);
    }
    if (root_obj.get("rate_limit")) |rate_limit| {
        switch (rate_limit) {
            .object => |obj| {
                if (obj.get("primary_window")) |window| {
                    snapshot.primary = parseWindow(window);
                }
                if (obj.get("secondary_window")) |window| {
                    snapshot.secondary = parseWindow(window);
                }
            },
            else => {},
        }
    }

    const has_credit_balance = if (snapshot.credits) |credits| credits.balance != null else false;
    if (snapshot.primary == null and snapshot.secondary == null and snapshot.reset_credits == null and !has_credit_balance) {
        if (snapshot.credits) |*credits| {
            if (credits.balance) |balance| allocator.free(balance);
        }
        return null;
    }

    return snapshot;
}

pub fn parseResetCreditExpiry(allocator: std.mem.Allocator, body: []const u8, now: i64) !?i64 {
    var parsed = try std.json.parseFromSlice(std.json.Value, allocator, body, .{});
    defer parsed.deinit();
    const root = switch (parsed.value) {
        .object => |obj| obj,
        else => return null,
    };
    const credits = switch (root.get("credits") orelse return null) {
        .array => |array| array,
        else => return null,
    };
    var earliest: ?i64 = null;
    for (credits.items) |credit| {
        const obj = switch (credit) {
            .object => |value| value,
            else => continue,
        };
        const status = switch (obj.get("status") orelse continue) {
            .string => |value| value,
            else => continue,
        };
        if (!std.mem.eql(u8, status, "available")) continue;
        const expires = switch (obj.get("expires_at") orelse continue) {
            .string => |value| value,
            else => continue,
        };
        const timestamp_ms = session.parseTimestampMs(expires) orelse continue;
        const timestamp = @divTrunc(timestamp_ms, 1000);
        if (timestamp <= now) continue;
        if (earliest == null or timestamp < earliest.?) earliest = timestamp;
    }
    return earliest;
}

fn parseResetCredits(v: std.json.Value) ?i64 {
    const obj = switch (v) {
        .object => |o| o,
        else => return null,
    };
    return parseIntValue(obj.get("available_count") orelse return null);
}

fn parseWindow(v: std.json.Value) ?registry.RateLimitWindow {
    const obj = switch (v) {
        .object => |o| o,
        else => return null,
    };

    const used_percent = if (obj.get("used_percent")) |used| parseFloatValue(used) else null;
    if (used_percent == null) return null;

    const window_minutes = if (obj.get("limit_window_seconds")) |seconds|
        if (parseIntValue(seconds)) |value| ceilMinutes(value) else null
    else
        null;
    const resets_at = if (obj.get("reset_at")) |reset_at| parseIntValue(reset_at) else null;

    return .{
        .used_percent = used_percent.?,
        .window_minutes = window_minutes,
        .resets_at = resets_at,
    };
}

fn parseCredits(allocator: std.mem.Allocator, v: std.json.Value) !?registry.CreditsSnapshot {
    const obj = switch (v) {
        .object => |o| o,
        else => return null,
    };

    const has_credits = if (obj.get("has_credits")) |value| switch (value) {
        .bool => |b| b,
        else => false,
    } else false;
    const unlimited = if (obj.get("unlimited")) |value| switch (value) {
        .bool => |b| b,
        else => false,
    } else false;
    const balance = if (obj.get("balance")) |value| try parseBalance(allocator, value) else null;

    return .{
        .has_credits = has_credits,
        .unlimited = unlimited,
        .balance = balance,
    };
}

fn parseBalance(allocator: std.mem.Allocator, value: std.json.Value) !?[]u8 {
    return switch (value) {
        .string => |s| if (s.len == 0) null else try allocator.dupe(u8, s),
        .integer => |i| try std.fmt.allocPrint(allocator, "{d}", .{i}),
        .float => |f| if (std.math.isFinite(f)) try std.fmt.allocPrint(allocator, "{d}", .{f}) else null,
        .number_string => |s| if (s.len == 0) null else try allocator.dupe(u8, s),
        else => null,
    };
}

fn parseFloatValue(value: std.json.Value) ?f64 {
    return switch (value) {
        .float => |f| if (std.math.isFinite(f)) f else null,
        .integer => |i| @as(f64, @floatFromInt(i)),
        .number_string, .string => |s| std.fmt.parseFloat(f64, s) catch null,
        else => null,
    };
}

fn parseIntValue(value: std.json.Value) ?i64 {
    return switch (value) {
        .integer => |i| i,
        .number_string, .string => |s| std.fmt.parseInt(i64, s, 10) catch null,
        .float => |f| if (std.math.isFinite(f) and @floor(f) == f and f >= @as(f64, @floatFromInt(std.math.minInt(i64))) and f <= @as(f64, @floatFromInt(std.math.maxInt(i64)))) @as(i64, @intFromFloat(f)) else null,
        else => null,
    };
}

fn parsePlanType(v: std.json.Value) ?registry.PlanType {
    const plan_name = switch (v) {
        .string => |s| s,
        else => return null,
    };

    return registry.normalizePlanType(plan_name);
}

fn ceilMinutes(seconds: i64) ?i64 {
    if (seconds <= 0) return null;
    return @divTrunc(seconds + 59, 60);
}

fn runUsageCommand(
    allocator: std.mem.Allocator,
    endpoint: []const u8,
    access_token: []const u8,
    account_id: []const u8,
) !UsageHttpResult {
    const result = try chatgpt_http.runGetJsonCommand(allocator, endpoint, access_token, account_id);
    return .{
        .body = result.body,
        .status_code = result.status_code,
    };
}
