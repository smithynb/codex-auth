const std = @import("std");
const row_data = @import("rows.zig");
const style = @import("style.zig");
const text_width = @import("../tui/text_width.zig");

pub const SwitchWidths = row_data.SwitchWidths;

pub const LiveListViewport = struct {
    start_row: usize = 0,
    max_rows: ?usize = null,
    max_cols: ?usize = null,
};

pub const column_count = 5;
const live_account_ident_width: usize = 10;
const live_account_suffix_min_width: usize = 10;
const live_account_suffix_min_len: usize = 3;
const live_account_prefix_min_len: usize = 6;

const LiveTableColumn = struct {
    header: []const u8,
    width: usize,
};

pub const Cell = struct {
    text: []const u8,
    indent: usize = 0,
};

pub const LiveTable = struct {
    columns: [column_count]LiveTableColumn,
    prefix_width: usize,

    pub fn writeHeader(self: *const LiveTable, writer: *style.StyledWriter) !void {
        try writer.writeStyle(style.role.status);
        try writeRepeat(writer.out, ' ', self.prefix_width);
        try self.writeCells(writer.out, &.{
            .{ .text = self.columns[0].header },
            .{ .text = self.columns[1].header },
            .{ .text = self.columns[2].header },
            .{ .text = self.columns[3].header },
            .{ .text = self.columns[4].header },
        });
        try writer.reset();
        try writer.writeAll("\n");
    }

    pub fn writeGroupRow(self: *const LiveTable, writer: *style.StyledWriter, account: []const u8) !void {
        try writer.writeStyle(style.role.secondary);
        try writeRepeat(writer.out, ' ', self.prefix_width);
        try writeAccountTruncatedPadded(writer.out, account, self.columns[0].width);
        try writer.reset();
        try writer.writeAll("\n");
    }

    pub fn writeDataRow(
        self: *const LiveTable,
        writer: *style.StyledWriter,
        prefix: []const u8,
        cells: [column_count]Cell,
        ansi_style: []const u8,
    ) !void {
        try writer.writeStyle(ansi_style);
        try writer.writeAll(prefix);
        if (prefix.len < self.prefix_width) {
            try writeRepeat(writer.out, ' ', self.prefix_width - prefix.len);
        }
        try self.writeCells(writer.out, &cells);
        if (ansi_style.len != 0) try writer.reset();
        try writer.writeAll("\n");
    }

    fn writeCells(
        self: *const LiveTable,
        out: *std.Io.Writer,
        cells: *const [column_count]Cell,
    ) !void {
        for (self.columns, 0..) |column, i| {
            if (i > 0) try out.writeAll("  ");
            const indent = @min(cells[i].indent, column.width);
            try writeRepeat(out, ' ', indent);
            if (i == 0) {
                try writeAccountTruncatedPadded(out, cells[i].text, column.width - indent);
            } else if (i == 2 or i == 3) {
                try writeRateLimitPadded(out, cells[i].text, column.width - indent);
            } else {
                try writeTruncatedPadded(out, cells[i].text, column.width - indent);
            }
        }
    }
};

fn writeRateLimitPadded(out: *std.Io.Writer, text: []const u8, width: usize) !void {
    if (text.len > width) {
        if (std.mem.indexOf(u8, text, "% (")) |percent_idx| {
            const meridiem_idx = std.mem.indexOf(u8, text, " AM") orelse std.mem.indexOf(u8, text, " PM");
            if (meridiem_idx) |idx| {
                // A shortened 12-hour clock must retain its AM/PM suffix.
                const clock_end = idx + 3;
                if (clock_end + 1 <= width) {
                    try out.writeAll(text[0..clock_end]);
                    try out.writeAll(")");
                    try writeRepeat(out, ' ', width - clock_end - 1);
                } else {
                    try writeTruncatedPadded(out, text[0 .. percent_idx + 1], width);
                }
                return;
            }
        }
    }
    try writeTruncatedPadded(out, text, width);
}

pub fn accountTable(widths: SwitchWidths, prefix_width: usize) LiveTable {
    return .{
        .columns = .{
            .{ .header = "ACCOUNT", .width = widths.email },
            .{ .header = "PLAN", .width = widths.plan },
            .{ .header = "5H", .width = widths.rate_5h },
            .{ .header = "WEEKLY", .width = widths.rate_week },
            .{ .header = "LAST", .width = widths.last },
        },
        .prefix_width = prefix_width,
    };
}

pub fn boundWidths(widths: SwitchWidths, prefix_width: usize, max_cols: ?usize) SwitchWidths {
    const cols = max_cols orelse return widths;
    const separator_width = 2 * (column_count - 1);
    if (cols <= prefix_width + separator_width) {
        return .{
            .email = 0,
            .plan = 0,
            .rate_5h = 0,
            .rate_week = 0,
            .last = 0,
        };
    }

    var remaining = cols - prefix_width - separator_width;
    var bounded = SwitchWidths{
        .email = 0,
        .plan = 0,
        .rate_5h = 0,
        .rate_week = 0,
        .last = 0,
    };

    growBoundedWidth(&remaining, &bounded.email, @min(widths.email, live_account_ident_width));
    growBoundedWidth(&remaining, &bounded.rate_5h, @min(widths.rate_5h, @max(@as(usize, 4), "5H".len)));
    growBoundedWidth(&remaining, &bounded.rate_week, @min(widths.rate_week, "WEEKLY".len));
    growBoundedWidth(&remaining, &bounded.plan, @min(widths.plan, "PLAN".len));
    growBoundedWidth(&remaining, &bounded.last, @min(widths.last, @as(usize, 3)));

    growBoundedWidth(&remaining, &bounded.rate_5h, widths.rate_5h);
    growBoundedWidth(&remaining, &bounded.rate_week, widths.rate_week);
    growBoundedWidth(&remaining, &bounded.plan, widths.plan);
    growBoundedWidth(&remaining, &bounded.last, widths.last);
    growBoundedWidth(&remaining, &bounded.email, widths.email);

    return bounded;
}

fn growBoundedWidth(remaining: *usize, current: *usize, target: usize) void {
    if (current.* >= target) return;
    const amount = @min(remaining.*, target - current.*);
    current.* += amount;
    remaining.* -= amount;
}

fn writePadded(out: *std.Io.Writer, value: []const u8, width: usize) !void {
    try out.writeAll(value);
    const value_width = text_width.displayWidth(value);
    if (value_width >= width) return;
    try out.splatByteAll(' ', width - value_width);
}

fn writeTruncatedPadded(out: *std.Io.Writer, value: []const u8, width: usize) !void {
    if (width == 0) return;
    if (text_width.displayWidth(value) <= width) {
        try writePadded(out, value, width);
        return;
    }
    if (width == 1) {
        try out.writeAll(".");
        return;
    }
    try out.writeAll(value[0..text_width.prefixByteLength(value, width - 1)]);
    try out.writeAll(".");
}

fn writeAccountTruncatedPadded(out: *std.Io.Writer, value: []const u8, width: usize) !void {
    if (width == 0) return;
    const value_width = text_width.displayWidth(value);
    if (value_width <= width) {
        try writePadded(out, value, width);
        return;
    }
    if (width < live_account_suffix_min_width) {
        try writeTruncatedPadded(out, value, width);
        return;
    }

    const max_suffix = width - live_account_prefix_min_len - 1;
    if (max_suffix < live_account_suffix_min_len) {
        try writeTruncatedPadded(out, value, width);
        return;
    }

    const suffix_len = @min(max_suffix, @max(live_account_suffix_min_len, value_width - 1 - live_account_prefix_min_len));
    const prefix_len = width - suffix_len - 1;
    try out.writeAll(value[0..text_width.prefixByteLength(value, prefix_len)]);
    try out.writeAll(".");
    try out.writeAll(value[text_width.suffixStart(value, suffix_len)..]);
}

fn writeRepeat(out: *std.Io.Writer, ch: u8, count: usize) !void {
    try out.splatByteAll(ch, count);
}
